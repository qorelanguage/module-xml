# -*- mode: qore; indent-tabs-mode: nil -*-
#! @file SoapHandler.qm SOAP handler module providing the SoapHandler class to be registered as a handler with the Qore HttpServer module

/*  SoapHandler.qm Copyright (C) 2012 - 2017 Qore Technologies, s.r.o.

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# make sure we have the required qore version
%requires qore >= 0.8.13

# requires XML functionality
%requires xml

# requires the WSDL module
%requires WSDL

# need mime definitions
%requires Mime >= 1.1

# need definitions from the HttpServerUtil
%requires HttpServerUtil >= 0.3.11

# need Util functions
%requires Util

# do not use $ for vars
%new-style

# require type declarations everywhere
%require-types

# do not ignore errors in call arguments
%strict-args

module SoapHandler {
    version = "0.2.6";
    desc = "SoapHandler module";
    author = "David Nichols <david@qore.org>";
    url = "http://qore.org";
    license = "MIT";
}

/** @mainpage SoapHandler Module

    @tableofcontents

    @section soaphandlerintro Introduction to the SoapHandler Module

    This module implements server-side support for the <a href="http://en.wikipedia.org/wiki/SOAP">SOAP protocol</a> for serialization/deserialization of message data.

    This module provides the @ref SoapHandler::SoapHandler "SoapHandler" class which can be used to provide an RPC handler for the HttpServer class provided by the HttpServer module.

    @section soaphandler_relnotes SoapHandler Release History

    @subsection soaphandler_0_2_6 SoapHandler 0.2.6
    - <b>Compatibility Warnings</b>
      - Because the SoapHandler module now supports HTTP bindings, it will only return
        WSDLs from GET requests if the URI path includes <tt>?wsdl</tt> at the end of the string
    - reimplemented operation to support multiple SOAP bindings, SOAP operations can be assigned to multiple bindings
    - HTTP binding support

    @subsection soaphandler_0_2_5 SoapHandler 0.2.5
    - added support for matching requests with soap action values
    - added the err_func argument so SOAP response serialization errors can be logged at the source
    - added support for debugging (verbose exception info/logging)
    - fixes to service creation and GET WSDL handling
    - Content-type in exceptional cases follows Soap version
    - added support for logging content of SOAP messages

    @subsection soaphandler_0_2_4 SoapHandler 0.2.4
    - updated to a user module

    @subsection soaphandler_0_2_3 SoapHandler 0.2.3
    - return correct soap fault structure according to request

    @subsection soaphandler_0_2_2 SoapHandler 0.2.2
    - soap 1.2 improvements

    @subsection soaphandler_0_2_1 SoapHandler 0.2.1
    - matched to new WSDL and HttpServer implementation

    @subsection soaphandler_0_2_0 SoapHandler 0.2.0
    - better WSDL support

    @subsection soaphandler_0_1_0 SoapHandler 0.1.0
    - initial WSDL-based SOAP support (still incomplete)
*/

#! main SoapHandler namespace
public namespace SoapHandler {
    #! SoapHandler implementation; to be registered as a request handler in the HttpServer class
    public class SoapHandler inherits public AbstractHttpRequestHandler {
        #! version of the SoapHandler implementation
        const Version = "0.2.6";

        #! @cond nodoc
        private {
            hash methods;

            # service name -> WebService objects
            hash wsh;

            # operation name -> WebService object
            hash owsh;

            # lookup service name -> alternate paths
            hash plh;

            # lookup alt path -> WebService object
            hash wsph;

            # soapaction map: action -> binding -> operation
            hash sam;

            # service -> binding -> soapaction
            hash psam;

            int loglevel;

            # if True then verbose exception info will be logged
            bool debug;

            # a closure/call reference to get the log message and/or process arguments in incoming requests
            *code getLogMessage;

            # for atomicity when adding / removing methods
            RWLock rwl();

            # hash of methods by path
            hash th;

            # path TreeMap for each HTTP method
            hash mapMethodPathToOperation;

            # soapaction -> method -> binding
        }
        #! @endcond

        #! creates the handler with the given method list
        /** @param auth an authentication object (use new AbstractAuthenticator() for no authentication)
            @param n_getLogMessage an optional closure or call reference to be called when an incoming request is received; if this is set then it will be called with the following arguments: a context hash (see HttpServer::AbstractHttpRequestHandler::handleRequest() for a description of the context hash), the method definition as passed in the methods argument to this constructor, and a reference to the arguments in this call
            @param dbg this parameter is set to @ref Qore::True "True", then additional information will be logged when errors occur
         */
        constructor(AbstractAuthenticator auth, *code n_getLogMessage, bool dbg = False) : AbstractHttpRequestHandler(auth) {
            getLogMessage = n_getLogMessage;
            debug = dbg;
        }

        #! adds a method to the handler dynamically
        /** @param ws the WebService object for the method
            @param op the WSOperation object for the web service operation the method corresponds to
            @param func a call reference, a closure, or a string function name to call with the deserialized arguments to the method; the return value will be serialized to SOAP according to the WSDL and sent back to the caller
            @param help optional help text for the method
            @param logopt log options which can be used by a custom logger (see the \a getLogMessage parameter in the constructor)
            @param cmark an optional "context marker" for the method
            @param path an optional path for the method (assumed to be the name of the service)
            @param err_func a call reference, a closure, or a string function name to call with error information if an exception is thrown with SOAP data
            @param altpath an alternate path for the service / WSDL
            @param binding SOAP binding name, leave empty to use the first assigned binding
         */
        addMethod(WebService ws, WSOperation op, any func, *string help, *int logopt, any cmark, *string path, any err_func, *string altpath, *string binding) {
            if (!func)
                throw "SOAP-SERVER-ADDMETHOD-PARAMETER-ERROR", sprintf("second argument is not a function name or callable value type (got: %y)", func);

            addMethodInternal(ws,
                                ( "operation" : op,
                                  "name"      : op.name,
                                  "binding"   : binding,
                                  "function"  : func,
                                  "err_func"  : err_func,
                                  "help"      : help,
                                  "logopt"    : logopt,
                                  "cmark"     : cmark,
                                  "path"      : path,
                                  "altpath"   : altpath,
                                ));
            #printf("methods=%N\n", methods);
        }

        #! turns on or off debugging; when debugging is enabled more verbose error messages are reported
        setDebug(bool dbg = True) {
            debug = dbg;
        }

        #! returns the current status of the debug flag
        bool getDebug() {
            return debug;
        }

        #! call to remove the given service
        removeService(string name) {
            rwl.writeLock();
            on_exit rwl.writeUnlock();

            if (wsh{name}) {
                # remove all operations for the given web service
                map delete owsh{$1.operation.name}, wsh{name}.listOperations();

                # remove alternate path mappings
                map delete wsph{$1}, keys plh{name};
                delete plh{name};

                # remove webservice mappings
                delete wsh{name};
            }

            # remove soapaction mappings
            map delete sam.$1, keys psam{name};

            # remove path soapaction mappings
            delete psam{name};

            # remove method link mappings
            map delete methods.$1, keys th{name};

            # remove path method mapping
            delete th{name};
        }

        #! @cond nodoc
        # don't reimplement this method; fix/enhance it in the module
        final private addMethodInternal(WebService ws, hash method) {
            #printf("DEBUG: SoapHandler::addMethodInternal() method: %N\n", method);
            OperationalBinding binding = method.operation.getBinding(method.binding);

            if (binding instanceof SoapBinding) {
                SoapBinding sb = cast<SoapBinding>(binding);

                # soap binding
                *string sa = sb.soapAction;
                if (sa)
                    method.soap_action = sa;

                rwl.writeLock();
                on_exit rwl.writeUnlock();

                if (sa) {
                    if (!sam{sa})
                        sam{sa} = method;
                    else if (sam{sa}.operation != method.operation)
                        throw "SOAPACTION-ERROR", sprintf("cannot register soapAction %y for operation %y because it is already registered for operation %y", sa, method.operation.name, sam{sa}.operation.name);
                }

                # map top-level element to method
                if (method.path) {
                    string path = method.path;

                    # add path / soapaction mapping for service removal
                    if (sa && !psam{path}{sa})
                        psam{path}{sa} = True;

                    # add link to WebService object
                    if (!wsh{path})
                        wsh{path} = ws;

                    if (method.altpath && !wsph{method.altpath}) {
                        # add lookup for altpath
                        plh{path}.(method.altpath) = True;
                        wsph{method.altpath} = ws;
                    }

                    # add a link for each operation as well
                    map owsh{$1.operation.name} = ws, ws.listOperations(), !owsh{$1.operation.name};

                    # add method links if they don't already exist
                    foreach string rn in (method.operation.getTopLevelRequestNames()) {
                        methods{rn} = method;
                        th{path}{rn} = True;
                    }
                }
                else {
                    # add method link if it doesn't already exist
                    map methods.$1 = method, method.operation.getTopLevelRequestNames();
                }

            } else if (binding instanceof HttpBinding) {
                HttpBinding hb = cast<HttpBinding>(binding);
                # http binding
                rwl.readLock();
                on_exit rwl.readUnlock();
                if (!mapMethodPathToOperation{hb.httpMethod}) {
                    mapMethodPathToOperation{hb.httpMethod} = new TreeMap();
                }
                method.parseUrlOnly = hb.input.urlEncoded || hb.input.urlReplacement;
                method.exactMatch = !hb.input.urlReplacement;
                method.path = ltrim(method.path ?? hb.location, "/");
                if (index(method.path, "?") >= 0) {
                    throw "SOAPACTION-ERROR", sprintf("cannot register path %y for operation %y because it contains parameters", method.path, method.operation.name);
                }
                mapMethodPathToOperation{hb.httpMethod}.put(method.path, method);
            }
            else if (!exists binding) {
                throw "SOAPACTION-ERROR", sprintf("cannot register operation %y because it has no binding", method.operation.name);
            } else {
                throw "SOAPACTION-ERROR", sprintf("cannot register operation %y because it has unsupported binding: %y", method.operation.name, binding.className());
            }

            #printf("DEBUG: SoapHandler::addMethodInternal() %N %N\n", method.name, method.path);
        }

        private *hash help(hash cx) {
            hash h;

            rwl.readLock();
            on_exit rwl.readUnlock();

            foreach string cmd in (keys methods) {
                h{cmd}.description = methods{cmd}.help;
                if (methods{cmd}.params)
                    h{cmd}.params = methods{cmd}.params;
            }
            return h;
        }

        # don't reimplement this method; fix/enhance it in the module
        final private log(hash cx, string str) {
            string msg = "SOAP ";
            if (exists cx.user)
                msg += sprintf("user %s ", cx.user);
            msg += sprintf("from %s: ", cx."peer-info".address_desc);
            msg += vsprintf(str, argv);

            call_function_args(cx.logfunc, msg);
        }

        /** Method is to be overriden to implement custom logging HTTP messages handled by this object.
            @par cx context passed from @ref HttpServer::HttpServer "HttpServer"
            @par msg information about message to be logged. There are keys 'reason' with value 'request', 'response' or 'error',
                 'header' (HTTP header, in casew ) and 'body' containing XML data in readable form, i.e. uncompressed and in case of
                 multipart message only the related part is passed. Method is executed before message is sent or after
                 has been received. Do not allow exception raising in method not to interrupt handler function.

            @par Example:
            @code{.py}
private nothing msglog(hash cx, hash msg) {
    log(sprintf("%s:\n%s\n", msg.reason, msg.body));
}
            @endcode

            @since %SoapHandler 0.2.5
        */
        private nothing msglog(hash cx, hash msg) {
            # implement functionality in descendant class
        }

        # don't reimplement this method; fix/enhance it in the module
        private hash makeSoapFaultResponse(hash cx, int code, string errLog, bool soap12, string err, string desc, *bool fmt) {
            hash o;
            if (soap12) {
                o = ENVELOPE_12_NS;
                o."soapenv:Envelope"."soapenv:Body" =
                    ("soapenv:Fault":
                     ("soapenv:Code": ("soapenv:Value": err),
                      "soapenv:Reason": ("soapenv:Text": desc)));
                      # optional Node, Role, Detail, see https://www.w3.org/TR/soap12/, 5.4 Fault
            }
            else {
                o = ENVELOPE_11_NS;
                o."soapenv:Envelope"."soapenv:Body" =
                    ("soapenv:Fault":
                      ("faultcode": err,
                       "faultstring": desc,
                       "desc": ""));
                # optional faultActor,  https://www.w3.org/TR/2000/NOTE-SOAP-20000508/ 4.4 SOAP fault
            }
            #TODO: http binding https://www.w3.org/TR/2007/REC-soap12-part2-20070427/#soapinhttp
            hash msg = (
                "code": code,
                "errlog": errLog,
                "body": make_xml(o, fmt ? XGF_ADD_FORMATTING: 0),
                "hdr": ("Content-Type" : WSDLLib::getSoapMimeType12(soap12)),
            );
            msglog(cx, ('reason': 'error', 'code': code, 'header': msg.hdr, 'body': msg.body));
            return msg;
        }

        # don't reimplement this method; fix/enhance it in the module
        final private *hash callOperation(hash cx, any args, hash method, bool reqsoap12) {

            # NOTE: internal methods have no operation definition and can take no parameters
            hash h;
            if (method.internal) {
                h.body = call_object_method(self, method.function);
                return h;
            }

            # add context marker, if any
            cx.cmark = method.cmark;
            cx.function = method.function;
            cx.operation = method.operation;

            try {
                if (method.parseUrlOnly) {
                    args = method.operation.deserializeRequestPath(cx.url.path, method.binding);
                } else {
                    args = method.operation.deserializeRequest(args, method.binding);
                }
            }
            catch (ex) {
                if (method.err_func) {
                    hash nex = ex;
                    nex.desc = sprintf("error deserializing incoming SOAP request: %s", ex.desc);
                    call_function(method.err_func, cx, nex);
                }
                string str = debug
                    ? get_exception_string(ex)
                    : sprintf("%s: %s: %s", get_ex_pos(ex), ex.err, ex.desc);
                return makeSoapFaultResponse(cx, 500, str, reqsoap12, ex.err, ex.desc);
            }

            if ((method.logopt & HttpServer::LP_LEVELMASK) <= loglevel && exists cx.logfunc) {
                string msg = sprintf("SOAP call from %s: %s", cx."peer-info".address_desc, method.name);

                if (method.logopt & HttpServer::LP_LOGPARAMS) {
                    msg += sprintf("(");
                    int i = 0;
                    foreach any arg in (args) {
                        if (inlist(i++, method.maskargs))
                            msg += "<masked>, ";
                        else if (arg.typeCode() == NT_HASH && elements arg) {
                            msg += "(";
                            foreach string k in (keys arg) {
                                if (k == method.maskkey)
                                    msg += sprintf("%s=<masked>, ", k);
                                else
                                    msg += sprintf("%s=%n, ", k, arg{k});
                            }
                            splice msg, -2, 2;
                            msg += "), ";
                        }
                        else
                            msg += sprintf("%n, ", arg);
                    }
                    # remove the last two characters from the string if any were added
                    if (i)
                        splice msg, -2, 2;
                    msg += ")";
                }

                log(cx, msg);
                h.logreply = True;
            }
            #printf("about to call function '%s' (method=%s params=%N)\n", method.function, method.name, args);flush();
            any rv;

            if (args.typeCode() == NT_LIST)
                unshift args, cx;
            else if (exists args)
                args = (cx, args);
            else
                args = cx;

            if (method.internal)
                rv = call_object_method_args(self, method.function, args);
            else
                rv = call_function_args(method.function, args);

            # if there is an output message, then serialize the response
            if (method.operation.output) {
                try {
                    any header = remove rv."^header^";
                    h += method.operation.serializeResponse(rv, header, NOTHING, NOTHING, reqsoap12, NOTHING, method.binding);
                    msglog(cx, ('reason': 'response', 'header': h.hdr, 'body': h.body));
                }
                catch (hash ex) {
                    if (method.err_func)
                        call_function(method.err_func, cx, ex);
                    rethrow;
                }
            }
            #log(cx, "body=%N", h.body);

            return h;
        }

        private *hash matchMethod(TreeMap tm, string path, reference unmatched) {
            bool hasParams = index(path, "?") >= 0;
            *string um;
            *hash method = tm.get(path, \um);
            if (method) {
                if (method.exactMatch) {
                    if (!hasParams && um != "" || hasParams && index(um, "?") > 0) {
                        return NOTHING;
                    }
                }
                unmatched = um;
            }
            return method;
        }

        # method called by HttpServer
        # don't reimplement this method; fix/enhance it in the module
        final hash handleRequest(hash cx, hash hdr, *data body) {
            #log(LL_DEBUG_1, "soap handler context=%y hdr=%y body=%y wsh=%y", cx, hdr, body, wsh.keys());
            #printf("soap handler context=%y hdr=%y body=%y keys wsh=%y\n", cx, hdr, body, wsh.keys()); #XXX
            cx.http_header = hdr;
            cx.http_body = body;
            *hash method;

            bool reqsoap12 = False; # set to True if soap 1.2 envelope is used in request

            *string path = cx.url.path;

            # remove leading / in path, if any
            path =~ s/^\///;

            if (hdr.method == "POST") {
                # try SoapBinding
                *string soapaction = (hdr."content-type" =~ x/;action=(.+)/)[0];
                if (soapaction)
                    hdr."content-type" =~ s/;.+$/;#/;    # TODO: is regex correct should not match till ;
                else
                    soapaction = hdr.soapaction;
                if (soapaction) {
                    rwl.readLock();
                    on_exit rwl.readUnlock();

                    method = sam{soapaction};
                    #printf("DEBUG: request matched with SoapAction %y\n", soapaction);
                }
            }

            if (!method) {
                # SOAP action is not explicitly specified, e.g. HTTP binding, try get it from path
                rwl.readLock();
                on_exit rwl.readUnlock();
                *string args;
                if (exists mapMethodPathToOperation{hdr.method}) {
                    TreeMap tm = mapMethodPathToOperation{hdr.method};
                    method = matchMethod(tm, path, \args);
                    if (!method) {
                        if (cx.root_path && path.equalPartialPath(cx.root_path)) {
                            method = tm.get(path.substr(cx.root_path.size()), \args);
                        }
                    }
                }
                if (method && method.parseUrlOnly) {
                    try {
                        msglog(cx, ('reason': 'request', 'method': hdr.method, 'path': path));  # we need log request
                        #printf("DEBUG: context: %N\nargs: %N\n", cx, args);
                        return ("code": 200) + callOperation(cx, args, method, reqsoap12);
                    }
                    catch (ex) {
                        string str = debug
                            ? get_exception_string(ex)
                            : sprintf("exception in %s: %s: %s", get_ex_pos(ex), ex.err, ex.desc);
                        return makeSoapFaultResponse(cx, 500, str, reqsoap12, ex.err, ex.desc);
                    }
                }
            }

            # service the WSDL with properly-formatted GET requests
            if (hdr.method == "GET" && path =~ /\?wsdl/i) {
                # path has not been resolved to an operation, so try an heuristic to match a service
                # remove query args if present
                path =~ s/\?.*$//;

                #printf("DEBUG: PATH=%N (%n %n)\n", path, exists wsh{path}, keys wsh);

                rwl.readLock();
                on_exit rwl.readUnlock();

                *WebService ws = tryMatch(path);
                if (!ws && cx.root_path && path.equalPartialPath(cx.root_path)) {
                    path = path.substr(cx.root_path.size());
                    # remove leading / in path, if any
                    path =~ s/^\///;
                    ws = tryMatch(path);
                }
                if (!ws) {
                    # try a match with the last component of the path, if any
                    path =~ s/.*\///g;
                    if (path)
                        ws = tryMatch(path);
                }

                if (!ws) {
                    return (
                        "code": 501,
                        "body": sprintf("invalid HTTP GET: no WebService object for path %y; known paths / services: %y; known operations: %y; known alt paths: %y", path, wsh.keys(), owsh.keys(), wsph.keys()),
                    );
                }

                # get URL for service
                # note that "socket-info".hostname is not returned for UNIX sockets
                string host;
                hash sih = cx."socket-info";
                if (sih.family == AF_UNIX) {
                    host = sprintf("socket=%s", encode_url(sih.address, True));
                }
                else {
                    host = sih.hostname;
                    if (host == "::" || host == "0.0.0.0")
                        host = gethostname();
                }

                string base_url = sprintf("http%s://%s", cx.ssl ? "s" : "", host);
                if (sih.port) {
                    int port = sih.port;
                    if (cx.ssl) {
                        if (port != 443)
                            base_url += ":" + port;
                    }
                    else if (port != 80)
                        base_url += ":" + port;
                }
                if (cx.root_path) {
                    base_url += "/" + cx.root_path;
                }

                return (
                    "code": 200,
                    "body": ws.getWSDL(base_url),
                    "hdr": ("Content-Type": "text/xml"),
                );
            }

            if (method || hdr.method == "POST") {
                # still chance to get method from xml element in case of SoapBinding (i.e.POST), we need parse message now
                any args;
                try {
                    *string ct = hdr."_qore_orig_content_type";
                    *string element = (ct =~ x/action=".*\/(.*)"/)[0];
                    # parse multipart messages
                    if (ct =~ /multipart\//) {
                        *string  x = (ct =~ x/multipart\/([^;]+)/)[0];
                        hdr."_qore_multipart" = x;
                        x = (ct =~ x/start=([^;]+)/)[0];
                        if (exists x)
                            hdr."_qore_multipart_start" = x;
                        x = (ct =~ x/boundary=([^;]+)/)[0];
                        if (exists x)
                            hdr."_qore_multipart_boundary" = x;
                        #printf("hdr=%N\n", hdr);
                    }
                    hash msg = WSDLLib::parseMultiPartSOAPMessage(hdr + ("body" : body));
                    msglog(cx, ('reason': 'request', 'header': msg.header, 'method': hdr.method, 'body': msg.body));
                    args = WSDLLib::parseSOAPMessage(msg);
                    if (WSDLLib::isSOAPMessage(msg)) {
                        # it is SOAP message (or XML content)
                        WSDL::XsdBase::removeNS(\args);
                        WSDL::XsdBase::removeNS(\args.Envelope);

                        if (!method) {
                            # get SOAP operation name from top XML element
                            if (!exists element && exists args.Envelope.Body && args.Envelope.Body.typeCode() == NT_HASH ) {
                                foreach string k in (keys args.Envelope.Body) {
                                    if (k == "multiRef" || k == "^attributes^")
                                        continue;
                                    element = (k =~ x/.*:(.*)/)[0];
                                    break;
                                }
                            }
                            if (element) {
                                rwl.readLock();
                                on_exit rwl.readUnlock();

                                method = methods{element};
                            }
                        }

                        # set soap version in request
                        *hash attr = args.Envelope."^attributes^";
                        foreach string k in (keys attr) {
                            if (k =~ /:soapenv$/) {
                                if (attr{k} == SOAP_12_ENV)
                                    reqsoap12 = True;
                                else if (attr{k} != SOAP_11_ENV)
                                    throw "SOAP-CALL-ERROR", sprintf("unsupported SOAP envelope received: %n", attr{k});
                                break;
                            }
                        }
                        if (!method && !exists element) {
                            throw "SOAP-CALL-ERROR", sprintf("no operation call found in message (SOAP body args: %y)", keys args.Envelope.Body);
                        }
                        #printf("DEBUG element: %y methods: %y args: %y\n", element, methods.keys(), args);
                    }
                }
                catch (ex) {
                    log(cx, "exception parsing SOAP msg: %y\n%y", body, ex);
                    string str = sprintf("exception in %s:%d: %s: %s (1: %N)", ex.file, ex.line, ex.err, ex.desc, ex.callstack)
                    #string str = debug
                        ? get_exception_string(ex)
                        : sprintf("%s: %s: %s", get_ex_pos(ex), ex.err, ex.desc);
                    return makeSoapFaultResponse(cx, 500, str, reqsoap12, ex.err, ex.desc);
                }

                if (method) {
                    try {
                        #printf("DEBUG: context: %N\nargs: %N\n", cx, args);
                        return ("code": 200) + callOperation(cx, args, method, reqsoap12);
                    }
                    catch (ex) {
                        string str = debug
                            ? get_exception_string(ex)
                            : sprintf("exception in %s: %s: %s", get_ex_pos(ex), ex.err, ex.desc);
                        return makeSoapFaultResponse(cx, 500, str, reqsoap12, ex.err, ex.desc);
                    }
                }
            }

            return (
                "code" : 501,
                "body" : sprintf("don't know how to handle request with method %y", hdr.method),
            );
        }

        private *WebService tryMatch(string path) {
            *WebService ws = wsh{path};
            if (!ws) {
                ws = owsh{path};
                if (!ws)
                    ws = wsph{path};
            }
            return ws;
        }
        #! @endcond
    }
}
