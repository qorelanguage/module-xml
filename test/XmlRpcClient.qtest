#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%require-types
%strict-args
%enable-all-warnings

%requires ../qlib/XmlRpcConnection.qm
%requires QUnit

%exec-class XmlRpcClientTest

class XmlRpcClientTest inherits QUnit::Test {
    constructor() : Test("XmlRpcClient test", "1.0") {
        addTestCase("XML-RPC client test", \testXmlRpcClient());
        addTestCase("XML-RPC connection test", \testXmlRpcConnection());

        # Return for compatibility with test harness that checks return value.
        set_return_value(main());
    }

    testXmlRpcClient() {
        XmlRpcClient client(("url": "http://localhost:80"), True);

        assertEq(True, client instanceof XmlRpcClient);
    }

    testXmlRpcConnection() {
        string url = "xmlrpcs://localhost:8099/";
        XmlRpcConnection conn("test", "test", url, False, NOTHING, parse_url(url));
        XmlRpcClient client = conn.get(False);
        string yurl = url;
        yurl =~ s/^xmlrpc/http/;
        assertEq(yurl, client.getURL());

        # test default path
        url = "xmlrpcs://localhost:8099";
        conn = new XmlRpcConnection("test", "test", url, False, NOTHING, parse_url(url));
        client = conn.get(False);
        yurl = url;
        yurl =~ s/^xmlrpc/http/;
        assertEq(yurl + "/RPC2", client.getURL());

        hash<ConnectionConstructorInfo> info = conn.getConstructorInfo();
        assertEq("xml", info.module);
        assertEq("Qore::Xml::XmlRpcClient", info.class_name);
        assertEq("https://localhost:8099", info.args[0].url);
        checkPreAndPostProcessing(info);
    }

    private checkPreAndPostProcessing(hash<ConnectionConstructorInfo> info) {
        Program p(PO_NEW_STYLE | PO_STRICT_ARGS | PO_REQUIRE_TYPES);
        if (info.module)
            p.loadModule(info.module);
        if (info.pre_processing) {
            string pre_processing = sprintf("sub pre_processing(reference<*softlist<auto>> args, bool connect, *hash rtopts) { %s }", info.pre_processing);
            # ensure that the pre processing code can be parsed
            p.parse(pre_processing, "pre processing");
            assertTrue(True, "pre processing: " + info.class_name);
        }
        if (info.post_processing) {
            string post_processing = sprintf("sub post_processing(%s obj, bool connect, *hash rtopts) { %s }", info.class_name, info.post_processing);
            # ensure that the post processing code can be parsed
            p.parse(post_processing, "post processing");
            assertTrue(True, "post processing: " + info.class_name);
        }
    }
}
