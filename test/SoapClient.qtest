#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

# make sure we have the right version of qore
%requires qore >= 0.8.13

%new-style
%require-types
%strict-args
%enable-all-warnings
%no-child-restrictions
%require-our

# test deprecated functions as well
%disable-warning deprecated

%requires QUnit
%requires HttpServer
%requires ../qlib/WSDL.qm
%requires ../qlib/SoapHandler.qm
%requires ../qlib/SoapClient.qm

%requires Util
%requires xml
%requires HttpServerUtil >= 0.3.11

const DefaultPort = 0;

%exec-class SoapClientTest

const ResponseBody = ("result": 99.9);

class TestSoapHandler inherits SoapHandler {
    public {
        list msglogs;
    }
    constructor(HttpServer::AbstractAuthenticator auth, *code n_getLogMessage, bool dbg = False): SoapHandler(auth, n_getLogMessage, dbg) {}

    nothing msglog(hash cx, hash msg) {
        push msglogs, msg;
    }
}

class TestSoapServer inherits HttpServer {
    public {
        TestSoapHandler soap;
        string opname;
        any mdata;
        any rdata;
    }

    private {
        int verbose;
    }

    constructor(WebService ws, int port, int verbose = 0) : HttpServer(\self.log(), \self.errlog(), verbose > 2) {
        self.verbose = verbose;

        # setup SOAP handler
        soap = new TestSoapHandler(new PermissiveAuthenticator(), NOTHING, verbose > 2);
        setHandler("soap_prefix", "SOAP", MimeTypeSoapXml, soap, "soapaction", False);
        setDefaultHandler("soap", soap);

        # setup operation handler
        soap.addMethod(ws, ws.getOperation("getInfo"), \getInfo(), NOTHING, NOTHING, NOTHING, "InfoService");

        # not yet supported
        soap.addMethod(ws, ws.getOperation("urlRepl"), \generalInputOnly(), NOTHING, NOTHING, NOTHING, /*path*/NOTHING, NOTHING, NOTHING, "b2");
        soap.addMethod(ws, ws.getOperation("urlEncoded1"), \generalInputOnly(), NOTHING, NOTHING, NOTHING, /*path*/NOTHING, NOTHING, NOTHING, "b2");
#        soap.addMethod(ws, ws.getOperation("urlEncoded2"), \generalInputOnly(), NOTHING, NOTHING, NOTHING, /*path*/NOTHING, NOTHING, NOTHING, "b2");
        soap.addMethod(ws, ws.getOperation("getMimeXml"), \generalOutputOnly(), NOTHING, NOTHING, NOTHING, /*path*/NOTHING, NOTHING, NOTHING, "b2");
        soap.addMethod(ws, ws.getOperation("getBinary"), \generalOutputOnly(), NOTHING, NOTHING, NOTHING, /*path*/NOTHING, NOTHING, NOTHING, "b2");

        soap.addMethod(ws, ws.getOperation("postForm"), \generalInputOnly(), NOTHING, NOTHING, NOTHING, /*path*/NOTHING, NOTHING, NOTHING, "b3");
        soap.addMethod(ws, ws.getOperation("postText"), \generalInputOnly(), NOTHING, NOTHING, NOTHING, /*path*/NOTHING, NOTHING, NOTHING, "b3");
        soap.addMethod(ws, ws.getOperation("postImg"), \generalInputOnly(), NOTHING, NOTHING, NOTHING, /*path*/NOTHING, NOTHING, NOTHING, "b3");
        soap.addMethod(ws, ws.getOperation("postImg2"), \generalInputOnly(), NOTHING, NOTHING, NOTHING, /*path*/NOTHING, NOTHING, NOTHING, "b3");
        soap.addMethod(ws, ws.getOperation("postImg3"), \generalInputOnly(), NOTHING, NOTHING, NOTHING, /*path*/NOTHING, NOTHING, NOTHING, "b3");
        soap.addMethod(ws, ws.getOperation("postData"), \generalInputOnly(), NOTHING, NOTHING, NOTHING, /*path*/NOTHING, NOTHING, NOTHING, "b3");

        addListener(port);
    }

    hash getInfo(hash cx, hash h) {
        if (h.tickerSymbol == "ERR")
            throw "ERR", "ERR";
        return (
            "body": ResponseBody,
            "docs": h.tickerSymbol,
            "logo": binary(cx.http_header.soapaction),
            );
    }

    *hash generalInputOnly(hash cx, hash h) {
        opname = cx.operation.name;
        mdata = h;
    }

    *hash generalOutputOnly(hash cx) {  # no input params
        opname = cx.operation.name;
        return rdata;
    }

    errlog(string fmt) {
        if (verbose > 2)
            vprintf(fmt + "\n", argv);
    }

    log(string fmt) {
        if (verbose > 2)
            vprintf(fmt + "\n", argv);
    }

}

class TestSoapClient inherits SoapClient {
    public {
        list msglogs;
    }
    constructor(hash h): SoapClient(h) {}
    nothing msglog(hash msg) {
        push msglogs, msg;
    }
}

class SoapClientTest inherits QUnit::Test {
    public {
        TestSoapServer server;
        TestSoapClient sc;
    }

    private {
        const WsdlUrl = "file://" + normalize_dir(get_script_dir()) + "/test.wsdl";

        #! command-line options
        const MyOpts = Opts + (
            "port": "p,port=i",
            #"verbose": "verbose=i",  # already in Opts
            "host": "host=s",
        );

        const OptionColumn = 25;
        string url;
        int serverPort;
    }

    constructor() : QUnit::Test("SoapClientTest", "1.0", \ARGV, MyOpts) {
        WebService ws(WSDLLib::getFileFromURL(WsdlUrl));

        # start SOAP server server
        server = new TestSoapServer(ws, m_options.port ?? DefaultPort, m_options.verbose);
        on_exit server.stop();
        serverPort = server.getListenerInfo(0).port;
        # when dual IPv4/IPv6 stack then may resolve gethostname() with unsupposed IP address where server is not listening
        url = sprintf("http://%s:%d", m_options.host ? m_options.host : gethostname(), serverPort);

        # add test cases
        addTestCase("SoapClientTest", \soapClientTest());
        addTestCase("HttpGetClientTest", \httpGetClientTest());
        addTestCase("HttpPostClientTest", \httpPostClientTest());
        addTestCase("SoapConnectionTest", \soapConnectionTest());

        # execute tests
        set_return_value(main()); exit();
    }

    cleanMsgLog() {
        sc.msglogs = ();
        server.soap.msglogs = ();
    }
    soapClientTest() {
        #printf("url: %y\n", url);
        sc = new TestSoapClient(("wsdl": WsdlUrl, "port": "SoapPort", "url": url));

        cleanMsgLog();

        hash h = sc.callOperation("getInfo", ("GetInfo": ("tickerSymbol": "ABC")), ("soapaction": ""));
        assertEq(99.9, h.body.result);
        assertEq("ABC", h.docs);
        assertEq(binary(), h.logo);
        assertEq("request", sc.msglogs[0].reason);
        assertEq(True, sc.msglogs[0].body =~ /GetInfo/);
        assertEq("response", sc.msglogs[1].reason);
        assertEq(True, sc.msglogs[1].body =~ /GetInfoResult/);
        assertEq(server.soap.msglogs[0].('reason', 'code', 'body'), sc.msglogs[0].('reason', 'code', 'body'));
        assertEq(server.soap.msglogs[1].('reason', 'code', 'body'), sc.msglogs[1].('reason', 'code', 'body'));

        cleanMsgLog();

        h = sc.callOperation("getInfo", ("GetInfo": ("tickerSymbol": "QOR")), ("soapaction": "TestAction"));
        assertEq(99.9, h.body.result);
        assertEq("QOR", h.docs);
        assertEq(binary("TestAction"), h.logo);

        cleanMsgLog();

        assertThrows("HTTP-CLIENT-RECEIVE-ERROR", \sc.callOperation(), ("getInfo", ("GetInfo": ("tickerSymbol": "ERR"))));
        assertEq("error", sc.msglogs[1].reason);
        assertEq(500, sc.msglogs[1].code);
        assertEq(True, sc.msglogs[1].body =~ /Fault/);
        assertEq("application/soap+xml,text/xml,application/xml", sc.msglogs[0]."accept");
        assertEq(server.soap.msglogs[0].('reason', 'code', 'body'), sc.msglogs[0].('reason', 'code', 'body'));
        assertEq(server.soap.msglogs[1].('reason', 'code', 'body'), sc.msglogs[1].('reason', 'code', 'body'));

        assertThrows("WSDL-OPERATION-ERROR", \sc.callOperation(), "xxx");

        WebService ws(WSDLLib::getWSDL(WsdlUrl));
        sc = new TestSoapClient(("wsdl": ws, "port": "SoapPort"));
        assertEq(True, sc instanceof SoapClient, "issue 1424");

        HTTPClient hc(("url": url));
        hash info;
        *string str = hc.get("/SOAP/InfoService?wsdl", NOTHING, \info);
        assertEq(True, str && str =~ /http:\/\/[^\/]*\/SOAP\/InfoService/, "issue 1631");
    }

    httpGetClientTest() {
        #printf("url: %y\n", url);
        sc = new TestSoapClient(("wsdl": WsdlUrl, "port": "HttpGetPort", "url": url));

        cleanMsgLog();
        *hash h;
        assertThrows("HTTP-CLIENT-RECEIVE-ERROR", \sc.callOperation(), ("urlRepl", ("country": "CZ", "city": "Prague 2", "zip": 12000)));
        assertEq("/get/CZ/12000-Prague%202", sc.msglogs[0].path);
        assertEq(sc.msglogs[0].method, "GET");

        try {
            cleanMsgLog();
            hash hi = ("country": "CZ", "city": "Prague_2", "zip": 12000);
            h = sc.callOperation("urlEncoded1", hi);
            assertEq(server.opname, "urlEncoded1");
            assertEq(server.mdata, hi);

            cleanMsgLog();
            server.rdata = ("STR": "AbCdEf");
            h = sc.callOperation("getMimeXml", ());
            assertEq(server.opname, "getMimeXml");
            assertEq(server.soap.msglogs[0].('reason', 'method'), sc.msglogs[0].('reason', 'method'));
            assertEq("text/xml;charset=UTF-8", sc.msglogs[1]."content-type");
            assertEq("text/xml", sc.msglogs[0]."accept");
            assertEq(server.rdata, h);

            cleanMsgLog();
            server.rdata = ("payload": ("^attributes^": ("^content-type^": "application/plain"), "^value^": "ADATA"));
            h = sc.callOperation("getBinary", ());
            assertEq(server.opname, "getBinary");
            assertEq("text/plain,text/html,image/*,application/*", sc.msglogs[0]."accept");
            assertEq(server.rdata.payload."^attributes^"."^content-type^", sc.msglogs[1]."content-type");
            server.rdata.payload."^value^" = binary(server.rdata.payload."^value^");
            assertEq(server.rdata, h);

            cleanMsgLog();
            server.rdata = ("payload": "xxx");
            assertThrows("HTTP-CLIENT-RECEIVE-ERROR", \sc.callOperation(), ("getBinary", ()));
        } catch (ex) {
            printf("Exception: %s\n", get_exception_string(ex));
            rethrow;
        }
    }

    httpPostClientTest() {
        #printf("url: %y\n", url);
        sc = new TestSoapClient(("wsdl": WsdlUrl, "port": "HttpPostPort", "url": url));
        *hash h;
        try {
            cleanMsgLog();
            hash hi = ("country": "CZ", "city": "Prague_2", "zip": 12000);
            h = sc.callOperation("postForm", hi);
            assertEq(server.opname, "postForm");
            assertEq(hi, server.mdata);
            assertEq(sc.msglogs[0].method, "POST");

            cleanMsgLog();
            hi = ("info": "very important text");
            h = sc.callOperation("postText", hi);
            assertEq(server.opname, "postText");
            assertEq("text/plain;charset=ISO-8859-1", sc.msglogs[0].header."Content-Type");
            assertEq(("info": ("^attributes^": ("^content-type^": "text/plain"), "^value^": hi.info)), server.mdata);

            cleanMsgLog();
            hi = ("img": "nice image");

            cleanMsgLog();
            hi = ("img": ("^attributes^": ("^content-type^": "image/png"), "^value^": <1234567890ABCDEF> ));
            h = sc.callOperation("postImg", hi);
            assertEq(server.opname, "postImg");
            assertEq(hi.img."^attributes^"."^content-type^", sc.msglogs[0].header."Content-Type");
            assertEq(hi, server.mdata);

            hi.img."^attributes^"."^content-type^" = "image/jpeg";
            assertThrows("SOAP-MESSAGE-ERROR", \sc.callOperation(), ("postImg", hi));

            cleanMsgLog();
            h = sc.callOperation("postImg2", hi);
            assertEq(server.opname, "postImg2");
            assertEq(hi.img."^attributes^"."^content-type^", sc.msglogs[0].header."Content-Type");
            assertEq(hi, server.mdata);

            cleanMsgLog();
            h = sc.callOperation("postImg3", hi);
            assertEq(server.opname, "postImg3");
            assertEq(hi.img."^attributes^"."^content-type^", sc.msglogs[0].header."Content-Type");
            assertEq(hi, server.mdata);

            cleanMsgLog();
            hi = ("payload": ("^attributes^": ("^content-type^": "application/qore"), "^value^": <0ABCDEF01234567890>));
            h = sc.callOperation("postData", hi);
            assertEq(server.opname, "postData");
            assertEq(hi.payload."^attributes^"."^content-type^", sc.msglogs[0].header."Content-Type");
            assertEq(hi, server.mdata);

        } catch (ex) {
            printf("Exception: %s\n", get_exception_string(ex));
            rethrow;
        }
    }

    soapConnectionTest() {
        string conn_url = WsdlUrl;
        conn_url =~ s/^file/soap/;

        if (m_options.verbose > 2)
            printf("conn_url: %y\n", conn_url);

        SoapConnection conn("test", "test", conn_url, False, ("target_url": url, "port": "SoapPort"), parse_url(conn_url));
        SoapClient sc2 = conn.get();
        hash h = sc2.callOperation("getInfo", ("GetInfo": ("tickerSymbol": "ABC")), ("soapaction": ""));
        assertEq(99.9, h.body.result);
        assertEq("ABC", h.docs);
        assertEq(binary(), h.logo);

        hash<ConnectionConstructorInfo> conn_info = conn.getConstructorInfo();
        assertEq("SoapClient", conn_info.module);
        assertEq("SoapClient::SoapClient", conn_info.class_name);
        assertEq(url, conn_info.args[0].target_url);
        assertEq(url, conn_info.args[0].url);
        assertEq(45s, conn_info.args[0].connect_timeout);
        assertEq(45s, conn_info.args[0].timeout);
        checkPreAndPostProcessing(info);
    }

    private checkPreAndPostProcessing(hash<ConnectionConstructorInfo> info) {
        Program p(PO_NEW_STYLE | PO_STRICT_ARGS | PO_REQUIRE_TYPES);
        if (info.module)
            p.loadModule(info.module);
        if (info.pre_processing) {
            string pre_processing = sprintf("sub pre_processing(reference<*softlist<auto>> args, bool connect, *hash rtopts) { %s }", info.pre_processing);
            # ensure that the pre processing code can be parsed
            p.parse(pre_processing, "pre processing");
            assertTrue(True, "pre processing: " + info.class_name);
        }
        if (info.post_processing) {
            string post_processing = sprintf("sub post_processing(%s obj, bool connect, *hash rtopts) { %s }", info.class_name, info.post_processing);
            # ensure that the post processing code can be parsed
            p.parse(post_processing, "post processing");
            assertTrue(True, "post processing: " + info.class_name);
        }
    }
}
